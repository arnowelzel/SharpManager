1 X=C*T+D
2 Y=(SIN X)^2
3 RETURN 
10 "N"RADIAN :PAUSE "NUM. INTEG."
20 INPUT "A=";A,"B=";B
30 C=(B-A)/2:D=(B+A)/2
40 T=.1834346425:GOSUB 1:Q=Y:T=-T:GOSUB 1:I=(Q+Y)*.3626837834
50 T=.5255324099:GOSUB 1:Q=Y:T=-T:GOSUB 1:I=(Q+Y)*.3137066459+I
60 T=.7966664774:GOSUB 1:Q=Y:T=-T:GOSUB 1:I=(Q+Y)*.2223810345+I
70 T=.9602898565:GOSUB 1:Q=Y:T=-T:GOSUB 1:I=(Q+Y)*.1012285363+I
80 X=I*C:PRINT "I=";X:END 
90 "K"PAUSE "ax^2+bx+c=0":INPUT "a=";B,"b=";C,"c=";D
100 "K1"IF B=0LET X=-D/C:PRINT "x=";X:END 
110 E=C*C-4*B*D:P=-C/2/B:Q=SQR(ABS E)/2/B
120 IF E=0LET X=P:PRINT "x=";X:END 
130 IF E>0LET X1=P+Q:X2=P-Q:PRINT "x1=";X1:PRINT "x2=";X2:END 
140 PRINT "x1,2=";P:PRINT " +-i ";Q:END 
150 "J"DEGREE :PAUSE "ax^3+bx^2+cx+d=0":INPUT "a=";A,"b=";B,"c=";C,"d=";D
160 IF A=0GOTO "K1"
170 E=B/A/3:Q=E^3-C*E/A/2+D/A/2:P=(3*A*C-B*B)/A/A/9:K=Q*Q+P^3
180 IF K>0GOTO 220
190 R=(SGN Q)*SQRABS P:W=ACS (Q/R^3)/3
200 X1=-2*R*COS W-E:X2=2*R*COS (60-W)-E:X3=2*R*COS (60+W)-E
210 PRINT "x1=";X1:PRINT "x2=";X2:PRINT "x3=";X3:END 
220 U=(-Q+SQRK)^(1/3):V=(-Q-SQRK)^(1/3):X1=U+V-E:X2=-(U+V)/2-E:X3=(U-V)*SQR3/2
230 PRINT "x1=";X1:PRINT "x2,3=";X2:PRINT " +-i ";X3:END 
240 "F"PAUSE "Y(X)=0":INPUT "I TACKA=";A,"II TACKA=";B
250 FOR I=1TO 100
260 X=A:GOSUB 2:C=Y:X=B:GOSUB 2:K=D:D=B-(B-A)*Y/(Y-C):A=B:B=D
270 IF ABS (K-D)<1E-6LET X=D:PRINT "X=";X:END 
280 NEXT I:PRINT "Nemogu da nadjem":END 
