10 FOR I=0TO W:FOR J=0TO Q
20 POKE 14388,I+49:POKE 14390,J+49
30 INPUT "A(1,1)=";B(I,J)
40 NEXT J:NEXT I:RETURN 
50 FOR I=0TO M:POKE 14441,I+49
60 INPUT "B2=";B(I,N):NEXT I:RETURN 
65 INPUT "DIMENZIJA  N=";N:M=N-1:RETURN 
66 USING "#.###^":RETURN 
70 "S"GOSUB 66:CLEAR :PAUSE "SISTEM LIN. JED"
75 GOSUB 65:DIM B(M,N)
80 PAUSE "KOFICIJENTI 
85 W=M:Q=M:GOSUB 10
90 GOSUB 50
100 FOR K=0TO M:Q=K+1:IF K=MGOTO 170
110 A=ABS B(K,K):L=K
120 FOR I=QTO M:IF A>=ABS B(I,K)GOTO 140
130 A=ABS B(I,K):L=I
140 NEXT I
150 IF L=KGOTO 170
160 FOR J=0TO N:P=B(K,J):B(K,J)=B(L,J):B(L,J)=P:NEXT J
170 IF B(K,K)=0PRINT "NEMA RES.":END 
180 FOR I=0TO M:IF I=KGOTO 200
190 FOR J=QTO N:B(I,J)=B(I,J)-B(I,K)/B(K,K)*B(K,J):NEXT J
200 NEXT I:NEXT K
210 FOR I=0TO M:PRINT "X";STR$ (I+1);"=";B(I,N)/B(I,I):NEXT I:END 
220 "M"GOSUB 66:CLEAR :PAUSE "MNOZENJE MAT"
230 INPUT "I MAT.(M*L) M=";M,"L=";L:M=M-1:L=L-1:DIM B(M,L):Q=L:W=M
240 GOSUB 10:INPUT "II MAT.(L*N) N=";N:N=N-1:DIM C(L,N):Q=N:W=L
260 POKE 14386,66:POKE 14395,67:GOSUB 10
270 POKE 14386,65:POKE 14395,66
280 FOR K=0TO M:FOR J=0TO N:D=0:FOR I=0TO L:D=D+B(K,I)*C(I,J):NEXT I
290 PRINT STR$ (K+1);",";STR$ (J+1);")=";D:NEXT J:NEXT K:END 
300 "A"GOSUB 66:CLEAR :PAUSE "INVERZ. MAT"
310 GOSUB 65:E=2*M+1:DIM B(M,E)
320 W=M:Q=M:GOSUB 10
330 FOR I=0TO M:FOR J=NTO E:IF J=N+IGOTO 350
340 B(I,J)=0:GOTO 355
350 B(I,J)=1
355 NEXT J:NEXT I
360 FOR K=0TO M:Q=K+1:IF K=MGOTO 430
370 A=ABS B(K,K):L=K
380 FOR I=QTO M:IF A>=ABS B(I,K)GOTO 400
390 A=ABS B(I,K):L=I
400 NEXT I
410 IF L=KGOTO 430
420 FOR J=0TO E:P=B(K,J):B(K,J)=B(L,J):B(L,J)=P:NEXT J
430 IF B(K,K)=0PRINT "NEMA INVER.MAT":END 
440 FOR I=0TO M:IF I=KGOTO 460
450 FOR J=QTO E:B(I,J)=B(I,J)-B(I,K)/B(K,K)*B(K,J):NEXT J
460 NEXT I:NEXT K
470 FOR I=0TO M:FOR J=1TO N
480 PRINT STR$ (I+1)+","+STR$ J+")=";B(I,M+J)/B(I,I)
490 NEXT J:NEXT I:END 
500 "D"GOSUB 66:CLEAR :PAUSE "DETERMINANTA"
510 GOSUB 65:DIM B(N,N),C(N):W=M:Q=M:GOSUB 10
520 R=1:FOR I=0TO M:IF B(I,I)=0GOTO 580
530 C(I)=B(I,I):FOR J=1TO M:B(I,J)=B(I,J)/C(I):NEXT J
540 E=I+1:FOR K=ETO M:C=B(K,I)
550 FOR J=ITO M:B(K,J)=B(K,J)-B(I,J)*C:NEXT J:NEXT K:NEXT I
560 D=R:FOR I=0TO M:D=D*C(I):NEXT I
570 PRINT "DET=";D:END 
580 E=I+1:FOR L=ETO M:IF B(L,I)=0GOTO 600
590 FOR J=0TO M:T=B(I,J):B(I,J)=B(L,J):B(L,J)=T:NEXT J:R=-R:GOTO 530
600 NEXT L:D=0:GOTO 570
610 "N"GOSUB 66:RADIAN :PAUSE "NUM. INTEG."
620 INPUT "A=";A,"B=";B
630 C=(B-A)/2:D=(B+A)/2
640 T=.1834346425:GOSUB 980:Q=Y:T=-T:GOSUB 980:I=(Q+Y)*.3626837834
650 T=.5255324099:GOSUB 980:Q=Y:T=-T:GOSUB 980:I=(Q+Y)*.3137066459+I
660 T=.7966664774:GOSUB 980:Q=Y:T=-T:GOSUB 980:I=(Q+Y)*.2223810345+I
670 T=.9602898565:GOSUB 980:Q=Y:T=-T:GOSUB 980:I=(Q+Y)*.1012285363+I
680 PRINT "I=";I*C:END 
700 "K"GOSUB 66:PAUSE "AX^2+BX+C=0":INPUT "A=";A,"B=";B,"C=";C:D=B*B-4*A*C:P=-B/2/A:Q=SQR(ABS D)/2/A
710 IF D=0PRINT "X=";P:END 
720 IF D>0PRINT "X1=";P+Q:PRINT "X2=";P-Q:END 
730 PRINT "RE(X)=";P:PRINT "IM(X)=";Q:END 
740 "F"GOSUB 66:PAUSE "Y(X)=0":INPUT "I TACKA=";A,"II TACKA=";B
745 FOR I=1TO 100
750 X=A:GOSUB 990:C=Y:X=B:GOSUB 990:K=D:D=B-(B-A)*Y/(Y-C):A=B:B=D
760 IF ABS (K-D)<1E-10PRINT "X=";D:END 
770 NEXT I:PRINT "NEMA RES.":END 
800 "X"GOSUB 66:CLEAR :PAUSE "SIS.NELIN.JED":INPUT "BR.JED=";N
802 DIM F(N),X(N),Z(N),Y(N)
804 FOR I=1TO N:PAUSE "X";I:INPUT X(I)
806 NEXT I
808 S=0:FOR I=1TO N:GOSUB 850:Y(I)=F(I):Z(I)=X(I):S=S+Y(I)*Y(I):NEXT I:S=SQRS
810 IF S<1E-4GOTO 836
812 T=1
814 FOR I=1TO N:X(I)=Z(I)+T*Y(I):NEXT I
816 A=0:GOSUB 850:FOR I=1TO N:A=A+F(I)*F(I):NEXT I:A=SQRA
818 IF A<STHEN 830
820 T=-T:IF T<=0GOTO 814
824 T=T/2:IF T>1E-8GOTO 814
828 PRINT "MET.DIVERGIRA":END 
830 S=A:FOR I=1TO N:Z(I)=X(I):Y(I)=F(I):NEXT I:J=J+1:GOTO 810
836 FOR I=1TO N:PRINT "X";I;"=";X(I):NEXT I:END 
850 F(1)=(20+.16*X(1))/(1-3.06E-4*X(1))-(26+.16*X(2))/(1-3.06E-4*X(2))
851 F(2)=-X(1)-X(2)+300+1.53E-4*(X(1)*X(1)+X(2)*X(2))
860 RETURN 
980 X=C*T+D
990 Y=SIN X/X
999 RETURN 
